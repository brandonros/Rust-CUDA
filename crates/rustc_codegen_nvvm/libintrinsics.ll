; This is a hand-written llvm ir module which contains extra functions
; that are easier to write. They mostly contain nvvm intrinsics that are wrapped in new
; functions so that rustc does not think they are llvm intrinsics and so you don't need to always use nightly for that.
;
; The LLVM 7 path uses the checked-in `libintrinsics.bc`. If you edit this file for the
; LLVM 7 path, regenerate the .bc with `llvm-as-7` (older or newer llvm-as will emit a
; bitcode format libnvvm rejects).
;
; The LLVM 19 path assembles this same source at build time with `llvm-as-19`; no
; regeneration required, just edit and rebuild.
source_filename = "libintrinsics"
; This data layout must match `DATA_LAYOUT` in `crates/rustc_codegen_nvvm/src/target.rs`.
target datalayout = "e-p:64:64:64-i1:8:8-i8:8:8-i16:16:16-i32:32:32-i64:64:64-i128:128:128-f32:32:32-f64:64:64-v16:16:16-v32:32:32-v64:64:64-v128:128:128-n16:32:64"
target triple = "nvptx64-nvidia-cuda"

; warp ----

define i32 @__nvvm_warp_size() #0 {
start:
  %0 = call i32 @llvm.nvvm.read.ptx.sreg.warpsize()
  ret i32 %0
}

declare i32 @llvm.nvvm.read.ptx.sreg.warpsize()

; other ----

define void @__nvvm_block_barrier() #1 {
start:
  call void @llvm.nvvm.barrier0()
  ret void
}

declare void @llvm.nvvm.barrier0()

define void @__nvvm_grid_fence() #1 {
start:
  call void @llvm.nvvm.membar.cta()
  ret void
}

declare void @llvm.nvvm.membar.cta()

define void @__nvvm_device_fence() #1 {
start:
  call void @llvm.nvvm.membar.gl()
  ret void
}

declare void @llvm.nvvm.membar.gl()

define void @__nvvm_system_fence() #1 {
start:
  call void @llvm.nvvm.membar.sys()
  ret void
}

declare void @llvm.nvvm.membar.sys()

define void @__nvvm_trap() #1 {
start:
  call void @llvm.trap()
  unreachable
  ret void
}

declare void @llvm.trap()

; math stuff -------------

define {i8, i1} @__nvvm_i8_addo(i8, i8) #0 {
start:
  %2 = sext i8 %0 to i16
  %3 = sext i8 %1 to i16
  %4 = call {i16, i1} @llvm.sadd.with.overflow.i16(i16 %2, i16 %3)
  %5 = extractvalue {i16, i1} %4, 0
  %6 = extractvalue {i16, i1} %4, 1
  %7 = trunc i16 %5 to i8
  %8 = insertvalue {i8, i1} undef, i8 %7, 0
  %9 = insertvalue {i8, i1} %8, i1 %6, 1
  ret {i8, i1} %9
}
declare {i16, i1} @llvm.sadd.with.overflow.i16(i16, i16) #0

define {i8, i1} @__nvvm_u8_addo(i8, i8) #0 {
start:
  %2 = sext i8 %0 to i16
  %3 = sext i8 %1 to i16
  %4 = call {i16, i1} @llvm.uadd.with.overflow.i16(i16 %2, i16 %3)
  %5 = extractvalue {i16, i1} %4, 0
  %6 = extractvalue {i16, i1} %4, 1
  %7 = trunc i16 %5 to i8
  %8 = insertvalue {i8, i1} undef, i8 %7, 0
  %9 = insertvalue {i8, i1} %8, i1 %6, 1
  ret {i8, i1} %9
}
declare {i16, i1} @llvm.uadd.with.overflow.i16(i16, i16) #0

define {i8, i1} @__nvvm_i8_subo(i8, i8) #0 {
start:
  %2 = sext i8 %0 to i16
  %3 = sext i8 %1 to i16
  %4 = call {i16, i1} @llvm.ssub.with.overflow.i16(i16 %2, i16 %3)
  %5 = extractvalue {i16, i1} %4, 0
  %6 = extractvalue {i16, i1} %4, 1
  %7 = trunc i16 %5 to i8
  %8 = insertvalue {i8, i1} undef, i8 %7, 0
  %9 = insertvalue {i8, i1} %8, i1 %6, 1
  ret {i8, i1} %9
}
declare {i16, i1} @llvm.ssub.with.overflow.i16(i16, i16) #0

define {i8, i1} @__nvvm_u8_subo(i8, i8) #0 {
start:
  %2 = sext i8 %0 to i16
  %3 = sext i8 %1 to i16
  %4 = call {i16, i1} @llvm.usub.with.overflow.i16(i16 %2, i16 %3)
  %5 = extractvalue {i16, i1} %4, 0
  %6 = extractvalue {i16, i1} %4, 1
  %7 = trunc i16 %5 to i8
  %8 = insertvalue {i8, i1} undef, i8 %7, 0
  %9 = insertvalue {i8, i1} %8, i1 %6, 1
  ret {i8, i1} %9
}
declare {i16, i1} @llvm.usub.with.overflow.i16(i16, i16) #0

define {i8, i1} @__nvvm_i8_mulo(i8, i8) #0 {
start:
  %2 = sext i8 %0 to i16
  %3 = sext i8 %1 to i16
  %4 = call {i16, i1} @llvm.smul.with.overflow.i16(i16 %2, i16 %3)
  %5 = extractvalue {i16, i1} %4, 0
  %6 = extractvalue {i16, i1} %4, 1
  %7 = trunc i16 %5 to i8
  %8 = insertvalue {i8, i1} undef, i8 %7, 0
  %9 = insertvalue {i8, i1} %8, i1 %6, 1
  ret {i8, i1} %9
}
declare {i16, i1} @llvm.smul.with.overflow.i16(i16, i16) #0

define {i8, i1} @__nvvm_u8_mulo(i8, i8) #0 {
start:
  %2 = sext i8 %0 to i16
  %3 = sext i8 %1 to i16
  %4 = call {i16, i1} @llvm.umul.with.overflow.i16(i16 %2, i16 %3)
  %5 = extractvalue {i16, i1} %4, 0
  %6 = extractvalue {i16, i1} %4, 1
  %7 = trunc i16 %5 to i8
  %8 = insertvalue {i8, i1} undef, i8 %7, 0
  %9 = insertvalue {i8, i1} %8, i1 %6, 1
  ret {i8, i1} %9
}
declare {i16, i1} @llvm.umul.with.overflow.i16(i16, i16) #0

; NVVM intrinsics return { i32, i1 }, but rustc lowering of (u32, bool) — or any
; small two-field aggregate — produces { i32, i8 }, which libnvvm rejects. We
; used to bridge by re-packing into { i32, i8 } here, but that aggregate return
; causes rustc's call-site ABI to attach `align N` to the return value, which
; LLVM 19's verifier rejects (align is only valid on pointer returns). So we
; pack into a plain i64 instead: low 32 bits = value, bit 32 = predicate.
; Primitive integer return ⇒ no struct ABI ⇒ no spurious return-attribute.

define i64 @__nvvm_warp_shuffle(i32, i32, i32, i32, i32) #1 {
start:
  %r = call { i32, i1 } @llvm.nvvm.shfl.sync.i32(i32 %0, i32 %1, i32 %2, i32 %3, i32 %4)
  %val = extractvalue { i32, i1 } %r, 0
  %pred = extractvalue { i32, i1 } %r, 1
  %val64 = zext i32 %val to i64
  %pred64 = zext i1 %pred to i64
  %pred_hi = shl i64 %pred64, 32
  %packed = or i64 %val64, %pred_hi
  ret i64 %packed
}

declare { i32, i1 } @llvm.nvvm.shfl.sync.i32(i32, i32, i32, i32, i32) #1

define i64 @__nvvm_warp_match_all_32(i32, i32) {
start:
  %r = call { i32, i1 } @llvm.nvvm.match.all.sync.i32(i32 %0, i32 %1)
  %val = extractvalue { i32, i1 } %r, 0
  %pred = extractvalue { i32, i1 } %r, 1
  %val64 = zext i32 %val to i64
  %pred64 = zext i1 %pred to i64
  %pred_hi = shl i64 %pred64, 32
  %packed = or i64 %val64, %pred_hi
  ret i64 %packed
}

declare { i32, i1 } @llvm.nvvm.match.all.sync.i32(i32, i32) #1

define i64 @__nvvm_warp_match_all_64(i32, i64) {
start:
  %r = call { i32, i1 } @llvm.nvvm.match.all.sync.i64(i32 %0, i64 %1)
  %val = extractvalue { i32, i1 } %r, 0
  %pred = extractvalue { i32, i1 } %r, 1
  %val64 = zext i32 %val to i64
  %pred64 = zext i1 %pred to i64
  %pred_hi = shl i64 %pred64, 32
  %packed = or i64 %val64, %pred_hi
  ret i64 %packed
}

declare { i32, i1 } @llvm.nvvm.match.all.sync.i64(i32, i64) #1

attributes #0 = { alwaysinline speculatable }
attributes #1 = { alwaysinline }
